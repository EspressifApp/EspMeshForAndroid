apply plugin: 'com.android.application'

Boolean willCompileAliyun = Boolean.valueOf(property('aliyun.compile'))
if (willCompileAliyun) {
    apply from: 'dependency.gradle'
}

android {
    compileSdkVersion conf.compile_sdk_version
    defaultConfig {
        applicationId "h5.espressif.esp32"
        minSdkVersion conf.min_sdk_version
        targetSdkVersion conf.target_sdk_version
        versionCode 47
        versionName "1.2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86"//过滤除 armeabi 和 x86 以外的 so 文件
        }
        multiDexEnabled true
    }
    signingConfigs {
        debug {
            if (Boolean.valueOf(property('sign.useInDebug'))) {
                storeFile file(property('sign.storeFile'))
                storePassword property('sign.storePassword')
                keyAlias property('sign.keyAlias')
                keyPassword property('sign.keyPassword')
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            if (Boolean.valueOf(property('sign.useInDebug'))) {
                signingConfig signingConfigs.debug
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            java {
                if (!willCompileAliyun) {
                    exclude 'aliyun/espressif/mesh/**'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation lib.appcompat
    implementation lib.material
    // RxJAVA
    implementation lib.rxjava
    implementation lib.rxandroid
    // QR Code
    implementation lib.zxing_android_embedded
    // EventBus
    implementation lib.eventbus

    // Modules
    implementation project(':appmesh')
}
